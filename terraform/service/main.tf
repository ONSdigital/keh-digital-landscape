# Create a service running on fargate with a task definition and service definition
terraform {
  backend "s3" {
    # Backend is selected using terraform init -backend-config=path/to/backend-<env>.tfbackend
    # bucket         = "sdp-dev-tf-state"
    # key            = "sdp-dev-ecs-example-service/terraform.tfstate"
    # region         = "eu-west-2"
    # dynamodb_table = "terraform-state-lock"
  }

}

# Create CloudWatch Log Groups beforehand
resource "aws_cloudwatch_log_group" "frontend_logs" {
  name              = "/ecs/ecs-service-${var.service_subdomain}-frontend"
  retention_in_days = var.log_retention_days
}

resource "aws_cloudwatch_log_group" "backend_logs" {
  name              = "/ecs/ecs-service-${var.service_subdomain}-backend"
  retention_in_days = var.log_retention_days
}

resource "aws_ecs_task_definition" "ecs_service_definition" {
  family = "ecs-service-${var.service_subdomain}-application"
  container_definitions = jsonencode([
    {
      # Frontend Container
      name      = "${var.service_subdomain}-task-application"
      image     = "${var.aws_account_id}.dkr.ecr.${var.region}.amazonaws.com/${var.frontend_ecr_repo}@${data.aws_ecr_image.frontend_image.image_digest}"
      cpu       = var.service_cpu / 2
      memory    = var.service_memory / 2
      essential = true
      portMappings = [
        {
          containerPort = var.frontend_port,
          hostPort      = var.frontend_port,
          protocol      = "tcp"
        }
      ],
      logConfiguration = {
        logDriver = "awslogs",
        options = {
          "awslogs-group"         = aws_cloudwatch_log_group.frontend_logs.name,
          "awslogs-region"        = var.region,
          "awslogs-stream-prefix" = "ecs"
        }
      },
      environment = [
        {
          name  = "VITE_BACKEND_URL",
          value = "https://${local.service_url}"
        },
        {
          name  = "VITE_SUPPORT_MAIL",
          value = var.support_mail
        },
        {
          name  = "VITE_ALERTS_CHANNEL_ID",
          value = var.alerts_channel_id
        },
        {
          name  = "IMAGE_TAG",
          value = data.aws_ecr_image.frontend_image.image_tag
        },
        {
          name  = "IMAGE_DIGEST",
          value = data.aws_ecr_image.frontend_image.image_digest
        }
      ]
    },
    {
      # Backend Container
      name      = "${var.service_subdomain}-backend"
      image     = "${var.aws_account_id}.dkr.ecr.${var.region}.amazonaws.com/${var.backend_ecr_repo}@${data.aws_ecr_image.backend_image.image_digest}"
      cpu       = var.service_cpu / 2
      memory    = var.service_memory / 2
      essential = true
      portMappings = [
        {
          containerPort = var.backend_port,
          hostPort      = var.backend_port,
          protocol      = "tcp"
        }
      ],
      environment = [
        {
          name  = "FRONTEND_URL",
          value = "https://${local.service_url}"
        },
        {
          name  = "NODE_ENV",
          value = "production"
        },
        {
          name  = "AWS_REGION",
          value = var.region
        },
        {
          name  = "PORT",
          value = tostring(var.backend_port)
        },
        {
          name  = "BUCKET_NAME",
          value = var.s3_bucket_name
        },
        {
          name  = "CLOUDWATCH_GROUP_NAME",
          value = "/ecs/ecs-service-${var.service_subdomain}-backend"
        },
        {
          name  = "TAT_BUCKET_NAME",
          value = var.api_s3_bucket_name
        },
        {
          name  = "GITHUB_APP_ID",
          value = var.github_app_id
        },
        {
          name  = "GITHUB_APP_CLIENT_ID",
          value = var.github_app_client_id
        },
        {
          name  = "GITHUB_APP_CLIENT_SECRET",
          value = var.github_app_client_secret
        },
        {
          name  = "AWS_SECRET_NAME",
          value = var.aws_secret_name
        },
        {
          name  = "GITHUB_ORG",
          value = var.github_org,
        },
        {
          name  = "COPILOT_BUCKET_NAME",
          value = var.copilot_bucket_name
        },
        {
          name  = "ALB_ARN",
          value = data.terraform_remote_state.ecs_infrastructure.outputs.application_lb_arn
        },
        {
          name  = "COGNITO_USER_POOL_ID",
          value = data.terraform_remote_state.ecs_auth.outputs.cognito_reviewer_user_pool_id
        },
        {
          name  = "COGNITO_USER_POOL_CLIENT_ID",
          value = data.terraform_remote_state.ecs_auth.outputs.cognito_reviewer_user_pool_client_id
        },
        {
          name  = "COGNITO_USER_POOL_DOMAIN",
          value = data.terraform_remote_state.ecs_auth.outputs.cognito_reviewer_user_pool_domain
        },
        {
          name  = "SIGN_OUT_URL",
          value = data.terraform_remote_state.ecs_auth.outputs.cognito_user_pool_sign_out_urls[0]
        },
        {
          name  = "IMAGE_TAG",
          value = data.aws_ecr_image.backend_image.image_tag
        },
        {
          name  = "IMAGE_DIGEST",
          value = data.aws_ecr_image.backend_image.image_digest
        }
      ],
      logConfiguration = {
        logDriver = "awslogs",
        options = {
          "awslogs-create-group"  = "true",
          "awslogs-group"         = aws_cloudwatch_log_group.backend_logs.name,
          "awslogs-region"        = var.region,
          "awslogs-stream-prefix" = "ecs",
          "mode"                  = "non-blocking"
        }
      },
      healthcheck = {
        command     = ["CMD-SHELL", "wget --no-verbose --tries=1 --spider http://localhost:${var.backend_port}/api/health || exit 1"]
        interval    = var.healthcheck_interval
        timeout     = var.healthcheck_timeout
        retries     = var.healthcheck_retries
        startPeriod = var.healthcheck_start_period
      },
      dependsOn = [
        {
          containerName = "${var.service_subdomain}-task-application"
          condition     = "START"
        }
      ]
    }
  ])
  execution_role_arn       = "arn:aws:iam::${var.aws_account_id}:role/ecsTaskExecutionRole"
  task_role_arn            = aws_iam_role.ecs_task_role.arn
  requires_compatibilities = ["FARGATE"]
  network_mode             = "awsvpc"
  cpu                      = var.service_cpu
  memory                   = var.service_memory
  runtime_platform {
    operating_system_family = "LINUX"
    cpu_architecture        = "X86_64"
  }
}

resource "aws_ecs_service" "application" {
  name             = "${var.service_subdomain}-service"
  cluster          = data.terraform_remote_state.ecs_infrastructure.outputs.ecs_cluster_id
  task_definition  = aws_ecs_task_definition.ecs_service_definition.arn
  desired_count    = var.task_count
  launch_type      = "FARGATE"
  platform_version = "LATEST"

  force_new_deployment               = var.force_deployment
  deployment_minimum_healthy_percent = 100
  deployment_maximum_percent         = 200

  enable_ecs_managed_tags = true # It will tag the network interface with service name
  wait_for_steady_state   = true # Terraform will wait for the service to reach a steady state before continuing

  # Add dependencies to ensure target groups are created first
  depends_on = [
    # Temporarily comment these out
    # Replace with site wide rules

    # aws_lb_listener_rule.tech_radar_authenticated_frontend_rule,
    # aws_lb_listener_rule.tech_radar_authenticated_backend_rule,
    # aws_lb_listener_rule.digital_landscape_copilot_api_rule,
    # aws_lb_listener_rule.digital_landscape_api_rule,
    # aws_lb_listener_rule.digital_landscape_frontend_rule

    aws_lb_listener_rule.digital_landscape_frontend_rule,
    aws_lb_listener_rule.digital_landscape_backend_rule_1,
    aws_lb_listener_rule.digital_landscape_backend_rule_2,
  ]

  load_balancer {
    target_group_arn = aws_lb_target_group.frontend_tg.arn
    container_name   = "${var.service_subdomain}-task-application"
    container_port   = var.frontend_port
  }

  load_balancer {
    target_group_arn = aws_lb_target_group.backend_tg.arn
    container_name   = "${var.service_subdomain}-backend"
    container_port   = var.backend_port
  }

  # We need to wait until the target group is attached to the listener
  # and also the load balancer so we wait until the listener creation
  # is complete first
  network_configuration {
    subnets         = data.terraform_remote_state.ecs_infrastructure.outputs.private_subnets
    security_groups = [aws_security_group.allow_rules_service.id]

    # TODO: The container fails to launch unless a public IP is assigned
    # For a private ip, you would need to use a NAT Gateway?
    assign_public_ip = true
  }

}

# Cloudwatch alarm that sounds when we have >0 A-ELB 5xx errors.
resource "aws_cloudwatch_metric_alarm" "application_elb_5xx_alarm" {
  alarm_name                = "Digital_Landscape_Application_ELB_5xx_alarm"
  comparison_operator       = "GreaterThanThreshold"
  evaluation_periods        = 1
  metric_name               = "HTTPCode_ELB_5XX_Count"
  namespace                 = "AWS/ApplicationELB"
  period                    = 60
  statistic                 = "Sum"
  threshold                 = 0
  alarm_description         = "Alarm when Application ELB produces 5xx Errors"
  insufficient_data_actions = []
  treat_missing_data        = "notBreaching"
  dimensions                = { LoadBalancer = "${var.domain}-service-lb" }
}


# Cloudwatch metric filter which checks if the backend health check endpoint is called, if so return 0, else add 1 to current failure count
resource "aws_cloudwatch_log_metric_filter" "backend_health_check_filter" {
  name           = "Digital_Landscape_backend_health_check_filter"
  pattern        = "Health check endpoint called"
  log_group_name = aws_cloudwatch_log_group.backend_logs.name

  metric_transformation {
    name          = "BackendHealthCheckFailureCount"
    namespace     = "ECS/ContainerInsights"
    value         = "0"
    default_value = "1"
  }
}


# Cloudwatch alarm that sounds when we have >0 health checks fail, or if there is no data every minute it sounds
resource "aws_cloudwatch_metric_alarm" "backend_health_check_alarm" {
  alarm_name          = "Digital_Landscape_backend_health_alarm"
  comparison_operator = "GreaterThanThreshold"
  evaluation_periods  = 1
  metric_name         = "BackendHealthCheckFailureCount"
  namespace           = "ECS/ContainerInsights"
  period              = 600
  statistic           = "Sum"
  threshold           = 0
  alarm_description   = "Alarm when ECS service has unhealthy tasks"
  treat_missing_data  = "breaching"
}